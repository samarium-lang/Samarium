@ Stack {

    create size:-/ * {
        'stack: [];
        'size: size;
    }

    push item * {
        ? 'isFull() { "stack is full (size " + 'size.toString() + ")"!!!; }
        'stack+: [item];
    }

    pushAll items * { ... element ->? items { 'push(element); } }

    special * { * 'stack$; }

    pop * {
        ? 'isEmpty() { "stack is empty"!!!; }
        out: 'stack<<-/>>;
        'stack-: 'stack$ - /;
        * out;
    }

    peek * {
        ? 'isEmpty() { "stack is empty"!!!; }
        * 'stack<<-/>>;
    }

    toBit * { * 'stack$ > \; }

    isFull * { * 'size :: 'stack$; }

    isEmpty * { * 'stack$ :: \; }

    toString * {
        <-string.wrap;
        ?? { top: 'peek(); }
        !! { top: _; }
        ? top?! :: ""?! { top: wrap(top.toString(), "\""); }
        * "Stack(capacity:" + 'size.toString()
        + ", size:" + 'stack$.toString()
        + ", top:" + top.toString()
        + ")";
    }
}

@ Queue {

    create size:-/ * {
        'queue: [];
        'size: size;
    }

    special * { * 'queue$; }
    isEmpty * { * ~~ '$; }
    isFull * { * '$ :: 'size; }

    throwEmpty * { ? 'isEmpty() { "queue is empty"!!!; } }

    put item * {
        ? 'isFull() { "queue is full (size " + 'size.toString() + ")"!!!; }
        'queue+: [item];
    }

    putAll items * {
        ... item ->? items { 'put(item); }
    }

    get * {
        'throwEmpty();
        out: 'queue<<\>>;
        'queue-: \;
        * out;
    }

    first * { 'throwEmpty(); * 'queue<<\>>; }
    last * { 'throwEmpty(); * 'queue<<-/>>; }

    toBit * { * '$ > \;}

    toString * {
        <-string.wrap;
        ? '$ <: /\/ {
            items: 'queue.toString();
        } ,, ? '$ > /\/ {
            first: 'first().toString();
            last: 'last().toString();
            ? 'first()?! :: ""?! { first: wrap(first, "\""); }
            ? 'last()?! :: ""?! { last: wrap(last, "\""); }
            items: "[" + first.toString() + ", ..., " + last.toString() + "]";
        } ,, {
            items: "[]";
        }
        * "Queue(capacity:" + 'size.toString()
        + ", size:" + 'queue$.toString()
        + ", items:" + items + ")";
    }
}

@ Set {

    create items:[] capacity:-/ * {
        <-iter.find;
        'find: find;
        'items: [i ... i ->? items] ? items ,, [];
        'capacity: capacity;
    }

    special * { * 'items$; }

    add value * {
        ? ~~ 'has(value) {
            ? 'isFull() {
                "set is full (capacity " + 'capacity.toString() + ")"!!!;
            }
            'items+: [value];
            * /;
        }
        * \;
    }

    toBit * { * ~~ 'isEmpty(); }

    toString * {
        * "Set(capacity:" + 'capacity.toString()
        + ", size:" + '$.toString()
        + ", items:" + 'items.toString()
        + ")";
    }

    remove value * { 'items-: [value]; }

    has value * { * 'find('items, value) >: \; }

    values * { * 'items; }

    clear * { 'items: []; }

    isEmpty * { * '$ :: \; }

    isFull * { * '$ :: 'capacity; }

    newSetSize a b * { * a$ ? a$ > b$ ,, b$; }

    union other * {
        ? other?! ::: Set()?! {
            "expected Set, received " + other?!.toString()!!!;
        }

        unionSet: Set('values(), 'newSetSize(', other) ++ /\);

        ... value ->? other.values() {
            unionSet.add(value);
        }

        * unionSet;
    }

    intersection other * {
        ? other?! ::: Set()?! {
            "expected Set, received " + other?!.toString()!!!;
        }

        intersectionSet: Set([], 'newSetSize(', other));

        ... value ->? 'values() {
            ? other.has(value) {
                intersectionSet.add(value);
            }
        }

        * intersectionSet;
    }

    difference other * {
        ? other?! ::: Set()?! {
            "expected Set, received " + other?!.toString()!!!;
        }

        differenceSet: Set([], 'newSetSize(', other));

        ... value ->? 'values() {
            ? ~~ other.has(value) {
                differenceSet.add(value);
            }
        }

        * differenceSet;
    }

    isSubset other * {
        ? other?! ::: Set()?! {
            "expected Set, received " + other?!.toString()!!!;
        }

        ? '$ > other$ { * \; }

        ... value ->? 'values() {
            ? ~~ other.has(value) { * \; }
        }
        * /;
    }
}

@ Deque {

    create size:-/ * {
        'deque: [];
        'size: size;
    }

    special * { * 'deque$; }
    isEmpty * { * ~~ '$; }
    isFull * { * '$ :: 'size; }

    throwEmpty * { ? 'isEmpty() { "deque is empty"!!!; } }
    throwFull * { ? 'isFull() { "deque is full (size " + 'size.toString() + ")"!!!; } }

    putFront item * { 'throwFull(); 'deque: [item] + 'deque; }
    put item * { 'throwFull(); 'deque+: [item]; }

    putFrontAll items * { ... item ->? items { 'putFront(item); } }
    putAll items * { ... item ->? items { 'put(item); } }

    front * { * 'deque<<\>>; }
    back * { * 'deque<<-/>>; }

    toBit * { * '$ > \; }

    getFront * {
        'throwEmpty();
        out: 'deque<<\>>;
        'deque-: \;
        * out;
    }

    get * {
        'throwEmpty();
        out: 'deque<<-/>>;
        'deque-: 'deque$ - /;
        * out;
    }

    toString * {
        <-string.wrap;
        ? '$ <: /\/ {
            items: 'deque.toString();
        } ,, ? '$ > /\/ {
            front: 'front().toString();
            back: 'back().toString();
            ? 'front()?! :: ""?! { front: wrap(front, "\""); }
            ? 'back()?! :: ""?! { back: wrap(back, "\""); }
            items: "[" + front.toString() + ", ..., " + back.toString() + "]";
        } ,, {
            items: "[]";
        }
        * "Deque(capacity:" + 'size.toString()
        + ", size:" + 'deque$.toString()
        + ", items:" + items + ")";
    }
}

@ StaticArray {

    create value type:_ * {
        ? value?! :: \?! {
            'value: {{}};
            'size: value;
            'type: type;
        } ,, ? value?! :: []?! {
            'value: {{}};
            'size: value$;
            'type: type;
            i: \;
            ... e ->? value {
                ? _ ::: type ::: e?! {
                    "expected " + type + ", found " + e?!.toString()!!!;
                }
                'value<<i>>: e;
                i+: /;
            }
        } ,, {
            "expected Integer or Array, found " + value?!.toString()!!!;
        }
    }

    special * { * 'value$; }

    toString * {
        * "StaticArray(size:" + 'size.toString()
        + ", type:" + 'type.toString()
        + ", items:" + 'value.toString()
        + ")";
    }

    toBit * { * 'value.toBit(); }

    throwInvalidIndex arg * {
        ? arg?! ::: \?! {
            "expected Integer, received " + arg?!.toString()!!!;
        }
        ? ~~ (\ <: arg < 'size) {
            "index " + arg.toString() + " is out of bounds (size " + 'size.toString() + ")"!!!;
        }
    }

    throwInvalidType type * {
        ? _ ::: 'type ::: type {
            "expected " + 'type + ", received " + type!!!;
        }
    }

    getItem index * {
        'throwInvalidIndex(index);
        * 'value<<index>>;
    }

    setItem index value * {
        'throwInvalidIndex(index);
        'throwInvalidType(value?!);
        'value<<index>>: value;
    }
}