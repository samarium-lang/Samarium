find array target * {
    index: \;
    ? array?! :: <-types.String {
        .. index + target$ <: array$ {
            ? array<<index..index+target$>> :: target {
                * index;
            }
            index+: /;
        }
    } ,, {
        ... e ->? array {
            ? e :: target {
                * index;
            }
            index+: /;
        }
    }
    * -/;
}

count array target * {
    ? target ~~ ->? array { * \; }
    s: \;
    ... e ->? array {
        ? e :: target {
            s+: /;
        }
    }
    * s;
}

cycle iter * {
    saved: [e ... e ->? iter];
    .. {
        ... e ->? saved {
            ** e;
        }
    }
}

filter function array * {
    ... e ->? array {
        ? function(e) {
            ** e;
        }
    }
}

filter_false function array * {
    flip arg * { * ~~ function(arg); }
    * filter(flip, array);
}

map function array * {
    ... e ->? array {
        ** function(e);
    }
}

reverse array * {
    i: array$ - /;
    .. i >: \ {
        ** array<<i>>;
        i-: /;
    }
}

take_while array function * {
    ... e ->? array {
        ? function(e) {
            ** e;
            ->
        }
        <-
    }
}

drop_while array function * {
    i: \;
    ... e ->? array {
        i+: /;
        ? ~~ function(e) {
            ** e;
            <-
        }
    }
    ... e ->? array<<i..array$>> {
        ** e;
    }
}

accumulate array function * {
    prev: array<<\>>;
    ** prev;
    ... e ->? array<</..>> {
        prev: function(prev, e);
        ** prev;
    }
}

reduce array function * {
    prev: array<<\>>;
    ... e ->? array<</..>> {
        prev: function(prev, e);
    }
    * prev;
}

pairwise array * {
    ... i ->? <<..array$ - />> {
        ** array<<i..i+/\>>;
    }
}

zip_longest fill arrays... * {
    longest: <-math.max([i$ ... i ->? arrays]);
    ... i ->? <<..longest>> {
        ... it ->? arrays {
            ?? {
                ** it<<i>>;
            } !! {
                ** fill;
            }
        }
    }
}

find_all array target * {
    ? array?! :: <-types.String && target$ > / {
        ... i ->? <<..array$>> {
            substring: array<<i..i + target$>>;
            ? substring :: target {
                ** i;
            }
        }
    } ,, {
        ... i, v ->? <<>> >< array {
            ? v :: target {
                ** i;
            }
        }
    }
}

all array * {
    ... i ->? array {
        ? ~~ i {
            * \;
        }
    }
    * /;
}

any array * {
    ... i ->? array {
        ? i {
            * /;
        }
    }
    * \;
}

chunks array size * {
    ... i ->? <<..array$..size>> {
        ** array<<i..i + size>>;
    }
}

#quicksort array * {
    x: [i ... i ->? array];
    ? x$ < /\ { * x; }
    pivot: x<<\>>;
    less: [i ... i ->? x<</..>> ? i <: pivot];
    greater: [i ... i ->? x<</..>> ? i > pivot];
    * #quicksort(less) + [pivot] + #quicksort(greater);
}

#dummy x * { * x; }

sorted array key? * {
    key <> #dummy;
    decorated: [[key(v), v] ... v ->? array];
    d_sorted: #quicksort(decorated);
    * [v ... _, v ->? d_sorted];
}

#flatten array * {
    flattened: [];
    ... i ->? array {
        ? i?! :: []?! {
            flattened+: i;
        } ,, {
            flattened+: [i];
        }
    }
    * flattened;
}

flatten array depth? * {
    depth <>;
    ? depth ::: && depth < \ {
        "depth cannot be negative"!!!;
    }
    x: [i ... i ->? array];

    ? depth :: \ {
        * x;
    }

    ? depth :: {
        old: x;
        new: #flatten(x);
        .. new ::: old {
            old: [i ... i ->? new];
            new: #flatten(old);
        }
        * new;
    } ,, {
        flattened: x;
        ... _ ->? <<..depth>> {
            flattened: #flatten(flattened);
        }
        * flattened;
    }
}