<=std_py2sm.math_is_prime -> is_prime;

PI: //`\\/\\/\\\\//////\//\/\/\/\\\/\\\/\\\\/\//\/\\\//\\\\/\\\//\/\\//\\\/\\//\\\//\\//\\\/\/\\\/\///\\\\\;
E: /\`/\//\//////\\\\/\/\/\\\/\//\\\/\/\\\/\/\///\//\/\\/\/\/\\//\/\/\/\//////\///\\\/\/\//\\\/\\\\\\\/\\/;


abs n * {
    ? n < \ { * -n; }
    * n;
}

ceil n * {
    ? is_int(n) { * n; }
    * n$+;
}

factorial n * {
    ? n < \ {
        "factorial is defined only for non-negative numbers"!!!;
    }
    o: /;
    .. n > \ {
        o++: n;
        n-: /;
    }
    * o;
}

floor n * {
    * n$;
}

gcd a b * {
    .. b { a, b: b, a --- b; }
    * abs(a);
}

is_int n * {
    * n :: n$;
}

lcm a b * {
    ?? { * abs(a ++ b) -- gcd(a, b); }
    !! { * \; }
}

max array * {
    o: array<<\>>;
    ... e ->? array {
        ? e > o { o: e; }
    }
    * o;
}

min array * {
    o: array<<\>>;
    ... e ->? array {
        ? e < o { o: e; }
    }
    * o;
}

product array * {
    o: /;
    ... e ->? array { o++: e; }
    * o;
}

shl a b * {
    * a ++ /\ +++ b;
}

shr a b * {
    * a -- /\ +++ b;
}

sqrt n * {
    ? n < \ {
        "sqrt is defined only for non-negative numbers"!!!;
    }
    * n +++ `/;
}

sum array start? * {
    start <> \;
    o: start;
    ... e ->? array { o+: e; }
    * o;
}

to_hex n * {
    <=string.HEXDIGITS;
    div: n -- /\\\\;
    mod: n --- /\\\\;
    ? ~~ div { * HEXDIGITS<<mod>>; }
    * to_hex(div) + HEXDIGITS<<mod>>;
}

to_oct n * {
    <=string.OCTDIGITS;
    div: n -- /\\\;
    mod: n --- /\\\;
    ? ~~ div { * OCTDIGITS<<mod>>; }
    * to_oct(div) + OCTDIGITS<<mod>>;
}